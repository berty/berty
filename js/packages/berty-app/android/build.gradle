// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        // minSdkVersion = 21 is required for optimal caching, see https://developer.android.com/studio/build/build-cache.html
        minSdkVersion = 21
        compileSdkVersion = 29
        targetSdkVersion = 29
        instabugUploadEnable = false // Fixed in `app/build.gradle`
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:3.5.3')

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven {
            url "https://sdks.instabug.com/nexus/repository/instabug-cp"
        }
    }
}

task importEnvFile {
    def envFile = file("${rootDir.getPath()}/../../../.env")

    if (envFile.exists()) {
        envFile.readLines().each() {
            if (!it.isEmpty() && !it.startsWith("#")) {
                def (key, value) = it.tokenize('=')
                if (key) {
                    System.properties[key] = (value ? value : '')
                }
            }
        }
    }
}

ext.getEnvValue = { key ->
    if (System.getenv(key)) { // Get var from system env first
        return System.getenv(key)
    } else if (System.getProperty(key)) { // if null, get var from env file
        return System.getProperty(key)
    }
    return "" // if null, return an empty string
}
